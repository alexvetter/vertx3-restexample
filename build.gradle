buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.1.1'
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

def vertxVersion = '3.0.0-SNAPSHOT'

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException("This needs Java 8.")
}

repositories {
    mavenCentral()
    maven {
        url = 'http://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile "io.vertx:vertx-core:${vertxVersion}"
    compile "io.vertx:vertx-web:${vertxVersion}"

    testCompile "io.vertx:vertx-unit:${vertxVersion}"
    testCompile 'junit:junit:4.12'
}

def mainVerticle = "vertx3.restexample.RestExampleVerticle"
mainClassName = "io.vertx.core.Starter"

run {
    args = ["run", mainVerticle]
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
        attributes 'Main-Verticle': mainVerticle
    }
}

shadowJar {
    classifier = 'fat'

    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
